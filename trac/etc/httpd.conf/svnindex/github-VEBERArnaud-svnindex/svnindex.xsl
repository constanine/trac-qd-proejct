<?xml version="1.0"?>

<!--
This file is part of the VEBERArnaud/svnindex package.

(c) Arnaud VEBER <arnaud@veber.pw>

For the full copyright and license information, please view the LICENSE
file that was distributed with this source code.
-->


<!-- A XML transformation style sheet for displaying the Subversion
  directory listing that is generated by mod_dav_svn when the "SVNIndexXSLT"
  directive is used. -->

<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" version="1.0">

  <xsl:output method="html"/>

  <xsl:template match="*"/>

  <xsl:template match="svn">
    <html>
      <head>
        <meta charset="utf-8"/>
        <meta http-equiv="X-UA-Compatible" content="IE=edge"/>
        <meta name="viewport" content="width=device-width, initial-scale=1"/>
        <title>
          <xsl:if test="string-length(index/@name) != 0">
            <xsl:value-of select="index/@name"/>
            <xsl:text>: </xsl:text>
          </xsl:if>
          <xsl:value-of select="index/@path"/>
        </title>
        <link rel="stylesheet" href="/svnindex/github-VEBERArnaud-svnindex/resources/bootstrap-3.3.4-dist/css/bootstrap.min.css"/>
        <!-- <link rel="stylesheet" type="text/css" href="/svnindex.css"/> -->
        <link href="/svnindex/github-VEBERArnaud-svnindex/resources/font-awesome-4.3.0/css/font-awesome.min.css" rel="stylesheet"/>
        
        <script src="/svnindex/github-VEBERArnaud-svnindex/resources/jquery-1.11.2.min.js"></script>
        <script src="/svnindex/github-VEBERArnaud-svnindex/resources/bootstrap-3.3.4-dist/js/bootstrap.min.js"></script>
      </head>
      <body>
        <nav class="navbar navbar-default navbar-fixed-top" role="navigation">
          <div class="container-fluid">
            <div class="navbar-header">
              <a class="navbar-brand" href="#">
                <span class="fa fa-fw fa-code-fork"></span>
                SVN of Yigo-support <a href="/svn/">Collection of Repositories</a>
              </a>
            </div>

            <div class="collapse navbar-collapse">
              <ul class="nav navbar-nav">
              </ul>
              <ul class="nav navbar-nav navbar-right">
              </ul>
            </div>
          </div>
        </nav>

        <xsl:apply-templates/>

        <nav class="navbar navbar-default navbar-fixed-bottom hidden-xs hidden-sm" role="navigation">
          <div class="container-fluid">
            <p class="navbar-text">
              Design by @VEBERArnaud
            </p>
            <p class="navbar-text navbar-right">
              <xsl:text>Powered by </xsl:text>
              <xsl:element name="a">
                <xsl:attribute name="href">
                  <xsl:value-of select="@href"/>
                </xsl:attribute>
                <xsl:text>Subversion</xsl:text>
              </xsl:element>
              <xsl:text> </xsl:text>
              <xsl:value-of select="@version"/>
            </p>
          </div>
        </nav>
      </body>
    </html>
  </xsl:template>

  <xsl:template match="index">
    <section class="jumbotron">
      <div class="container-fluid">
        <div class="page-header">
          <h1>
            <span class="fa fa-fw fa-sitemap hidden-xs hidden-sm"></span>
            <span class="hidden-xs hidden-sm">&#160;</span>
            <xsl:value-of select="@name"/>
            <xsl:value-of select="@base"/> 
            <small>
              <xsl:text> Revision </xsl:text>
              <xsl:value-of select="@rev"/>
            </small>
          </h1>
        </div>
      </div>
    </section>

    <section class="container-fluid">
      <div class="row">
        <div class="col-xs-12">
          <div class="breadcrumb">
            <h4>
              <span class="fa fa-fw fa-road"></span>
              <span>&#160;</span>
              <xsl:value-of select="@name"/>
              <a id="breadcrumb-base" href="#"><xsl:value-of select="@base"/></a>
                  <script>$("#breadcrumb-base").attr("href", "/svn/"+$("#breadcrumb-base").text());</script>
              <span id="breadcrumb-path"><xsl:value-of select="@path"/></span>
                  <script><![CDATA[
                  (function(){
                    var hrefBase = $("#breadcrumb-base").attr("href")
                    var path = $("#breadcrumb-path").text();
                    if (!path){
                        path="";
                    }
                    if (path.indexOf("/")!=0){
                        return;     //The root url:  /svn , path is "Collection of Repositories"
                    }
                    path=path.substr(1);
                    if (path){
                        var paths = path.split("/");
                        var hrefs = [];
                        for (var i=0; i < paths.length; i++){
                            if (i==0){
                                hrefs[i]=hrefBase+"/"+encodeURIComponent(paths[i]);
                            }else{
                                hrefs[i]=hrefs[i-1]+"/"+encodeURIComponent(paths[i]);
                            }
                        }
                        //Append links into #breadcrumb-path
                        var links = [];
                        for (var i=0; i < hrefs.length; i++){
                            if (i<hrefs.length-1){
                                links[i] = "<a href='"+hrefs[i]+"'>"+paths[i]+"</a>";
                            }else{
                                links[i] = paths[i];    //No link for current dir
                            }
                        }
                        var html = ":/"+links.join("/");
                        $("#breadcrumb-path").text("");
                        $("#breadcrumb-path").html(html);
                    }
                  })();
                  ]]></script>
            </h4>
          </div>
          <div class="list-group" style="padding-bottom:60px">
            <xsl:apply-templates select="updir"/>
            <xsl:apply-templates select="dir"/>
            <xsl:apply-templates select="file"/>
          </div>
        </div>
      </div>
    </section>
  </xsl:template>

  <xsl:template match="updir">
      <xsl:element name="a">
	<xsl:attribute name="class">list-group-item updir</xsl:attribute>
        <xsl:attribute name="href">..</xsl:attribute>
        <span class="fa fa-fw fa-level-up"></span>
        &#160;
        <xsl:text>..</xsl:text>
      </xsl:element>
  </xsl:template>

  <xsl:template match="dir">
      <xsl:element name="a">
	<xsl:attribute name="class">list-group-item dir</xsl:attribute>
        <xsl:attribute name="href">
          <xsl:value-of select="@href"/>
        </xsl:attribute>
        <span class="fa fa-fw fa-folder-o"></span>
        &#160;
        <xsl:value-of select="@name"/>
        <xsl:text>/</xsl:text>
      </xsl:element>
  </xsl:template>

  <xsl:template match="file">
      <xsl:element name="a">
	<xsl:attribute name="class">list-group-item file</xsl:attribute>
        <xsl:attribute name="href">
          <xsl:value-of select="@href"/>
        </xsl:attribute>
        <span class="fa fa-fw fa-file-o"></span>
        &#160;
        <xsl:value-of select="@name"/>
      </xsl:element>
  </xsl:template>

</xsl:stylesheet>
